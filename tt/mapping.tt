#nop Create map as we get it.

#alias {mapping.onConnected}
{
   #map create 100000;
   #map goto 1;
   #map flag static;
   #map flag unicode;
   #map flag nofollow;

   #nop seems to be either to use draw or vtmpa/split/offset;
   #draw boxed map 1 1 $uimap[height] $uimap[width];

   #nop#map read gmcp.map;
}

#alias {mapping.onRoomInfo}
{
   #map goto {$gmcp[room][info][num]} {dig};

   #nop Coords;
   #nop first number is the Map Id;
   #nop then horizton;
   #nop then vertical;
   #nop #VARIABLE {gmcp[room][info]};
   #nop     {area} {Ashtan};
   #nop     {coords} {49,6,-9,0};

   #nop #list gmcp[room][info][coords] {explode} {,};
   #nop to convert into a list;

   #map get roomvnum {result};
   #if {"$result" == ""}
   {
      #map set roomvnum $gmcp[room][info][num];
      #map set roomarea $gmcp[room][info][coords];
      #map set roomcolor <178>;
      #nop map set roomdata;
      #nop map set roomdesc;
      #nop map set roomflags;
      #nop map set roomid;
      #map set roomname $gmcp[room][info][name];
      #map set roomnote $gmcp[room][info][environment];
      #map set roomterr $gmcp[room][info][terrain];

      #nop ???;
      #map set roominfo $gmcp[room][info][details];

    };

   #foreach {*gmcp[room][info][details][]} {key}
   {
      #nop #show {<003>Room Details: $gmcp[room][info][details][$key]};
      #switch {{$gmcp[room][info][details][$key]}}
      {
         #case {"subdivision"}   {#map set roomsymbol {S};};
         #case {"bank"}          {#map set roomsymbol {B};};
         #case {"sewer"}         {#map set roomsymbol {G};};
         #case {"wilderness"}    {#map set roomsymbol {W};};
         #case {"shop"}          {#map set roomsymbol {$};};
      };
   };
    #foreach {*gmcp[room][info][exits][]} {exit}
    { 
      #map get {roomexit} {result};
      #if {&result[$exit] == 0}
      {
         #map get {roomvnum} {result} {$gmcp[room][info][exits][$exit]};
         #map dig {$exit} {$gmcp[room][info][exits][$exit]};
         #nop "map dig <direction> <roomnum>;
         #if {$result == 0}
         {
            #map set {roomcolor} {<118>} {$gmcp[room][info][exits][$exit]};
         }
      }
   };

   #draw boxed map 1 1 $uimap[height] $uimap[width];

   #math width {$uimap[width] - 4};

   #format {prompt} {%.${width}s} {<038>$gmcp[room][info][area]};
   #line ignore #showme {$prompt} {1} {3};

   #format {prompt} {%.${width}s} {<038>$gmcp[room][info][name]};
   #line ignore #showme {$prompt} {$uimap[height]} {3};

   #variable {exits} {};
   #foreach {n;e;s;w;u;d;in;out;nw;ne;se;sw} {exit}
   {
       #if {&gmcp[room][info][exits][$exit] == 0} { #continue };
       #variable {exits} {$exits<128>$exit<270>,};
   };

   #screen clear square {$uimap[height]+1} 1 {$uimap[height]+1} {$uimap[width]-1};
   #format {prompt} {%u} {$exits<099>};
   #line ignore #showme {$prompt} {$uimap[height]+1} {2};
   #unvar exits;
}


