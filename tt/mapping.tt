#nop Create map as we get it.

#alias {mapping.onConnected}
{
   #map create;
   #map flag static;
   #map flag unicode;
   #map flag nofollow;
   #map read world.map;

   #nop seems to be either to use draw or vtmpa/split/offset;
   #draw boxed map 1 1 $uimap[height] $uimap[width];
}

#alias {mapping.explore} {

   #list yetToVisit clear;

#nop Map Events;
#nop END OF PATH,  END OF RUN;

   #local coords $gmcp[room][info][coords];
   #list coords explode ,;
   #local areaId $coords[1];
   #map list {roomid}{$areaId} {variable}{rooms};
   #foreach *rooms[] roomid {
      #list yetToVisit ins 1 $roomid;
   };

   #unvar rooms;
   #unvar roomid;
}

#alias {mapping.onRoomInfo}
{
   #nop Get current Area ID;
   #local coords $gmcp[room][info][coords];
   #list coords explode ,;
   #local areaId $coords[1];

   #nop If we have left the current area, hide the old place.;
   #if {&{oldAreaId} && $oldAreaId != $areaId} {
      #map list {roomid}{$oldAreaId} {variable}{area};
      #foreach *area[] roomid {#map set {roomflags}{2} $roomid};
      #unvar area;
      #unvar roomid;
   };

   #map goto {$gmcp[room][info][num]};

   #nop On entering the new place, unhide it.;
   #map list {roomid}{$areaId} {variable}{area};
   #foreach *area[] roomid {#map set {roomflags}{0} $roomid};
   #unvar area;
   #unvar roomid;

   #draw boxed map 1 1 $uimap[height] $uimap[width];

   #math width {$uimap[width] - 4};

   #nop Put the area name at the top of the map;
   #format {prompt} {%.${width}s} {<038>$gmcp[room][info][area]};
   #line ignore #showme {$prompt} {1} {3};

   #nop Put the room name at the bottom of the map;
   #format {prompt} {%.${width}s} {<038>$gmcp[room][info][name]};
   #line ignore #showme {$prompt} {$uimap[height]} {3};

   #nop Display the exits;
   #variable {exits} {};
   #foreach {n;e;s;w;u;d;in;out;nw;ne;se;sw} {exit}
   {
       #if {&gmcp[room][info][exits][$exit] == 0} { #continue };
       #variable {exits} {$exits<128>$exit<270>,};
   };

   #screen clear square {$uimap[height]+1} 1 {$uimap[height]+1} {$uimap[width]-1};
   #format {prompt} {%u} {$exits<099>};
   #line ignore #showme {$prompt} {$uimap[height]+1} {2};
   #unvar exits;

   #nop Save the old area to be hidden when we leave.;
   #var oldAreaId $areaId;
}


