#nop Gui Setup and chats/messages

#VARIABLE {comm_window}
{
    {active} {chats}
    {botcol} {150}
    {botrow} {10}
    {offset} {1}
    {tab} 
    {
        {chats} {}
        {tells} {}
    }
    {topcol} {30}
    {toprow} {1}
    {unread} 
    {
        {chats} {0}
        {tells} {0}
    }
}
#ACTION {~%1 tells you %2}
{
    comm_window_show tells %1 tells you %2
}
#nop #ACTION {~%1 says, %2} { comm_window_show chats %1 chats %2; };

#ALIAS {comm_window_scroll_up}
{
    #if {$comm_window[offset] < &comm_window[tab][%0][]}
    {
        #math comm_window[offset] $comm_window[offset] + 1;
        comm_window_draw_data $comm_window[active]
    }
}

#ALIAS {comm_window_scroll_down}
{
    #if {$comm_window[offset] > 1}
    {
        #math comm_window[offset] $comm_window[offset] - 1;
        comm_window_draw_data $comm_window[active]
    }
}

#BUTTON {$comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol] SCROLLED MOUSE WHEEL UP}
{
    comm_window_scroll_up $comm_window[active]
}

#BUTTON {$comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol] SCROLLED MOUSE WHEEL DOWN}
{
    comm_window_scroll_down
}

#FUNCTION {comm_window_square}
{
    #return $comm_window[toprow]+1 $comm_window[topcol]+1 $comm_window[botrow]-1 $comm_window[botcol]-1
}

#FUNCTION {comm_window_border}
{
    #return $comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol]
}

#ALIAS {comm_window_draw_data}
{
    #draw tile @comm_window_square{} $comm_window[tab][%1][%*]
}

#ALIAS {comm_window_show %1 %2}
{
    #list comm_window[tab][%1] ins -1 {%2};
    #if {{%1} === {$comm_window[active]}}
    {
        comm_window_draw_data %1
    };
    #elseif {$comm_window[unread][%1] == 0}
    {
        #variable comm_window[unread][%1] 1;
        comm_window_draw_tabs
    }
}

#ALIAS {comm_window_draw_tabs}
{
   #draw green filled jeweled box @comm_window_border{};

   #local short {};
   #local index {};
   #loop {1} {&comm_window[tab][]} {index}
   {
      #local tab *comm_window[tab][+$index];
      #if {{$comm_window[active]} === {$tab}}
      {
          #format short <138>%.6s $tab
      };
      #elseif {$comm_window[unread][$tab] == 1}
      {
          #format short <568>%.6s $tab
      };
      #else
      {
          #format short <268>%.6s $tab
      };
      #line ignore #showme {\e]68;2;TAB;comm_window_tab_click $tab\a\e[4;24m$short\e[24m} {$comm_window[toprow]} {$comm_window[topcol] - 5 + $index * 7};

   }
}

#ALIAS {comm_window_tab_click}
{
    #variable comm_window[active] %0;
    #variable comm_window[unread][%0] 0;
    #variable comm_window[offset] 1;
    comm_window_draw_tabs;
    comm_window_draw_data %0
}

#alias {schats} {
   comm_window_tab_click chats
}

#alias {stells} {
   comm_window_tab_click tells
}

#EVENT {PRESSED SECURE LINK TAB MOUSE BUTTON ONE}
{
    %4
}

#ALIAS {guireset}
{
   #nop #config mouse on;
   #split 10 1 30 39;
   comm_window_draw_tabs;
}

#ALIAS {test}
{
    #nop #split 6 1;
    #showme <201>Make there is a split of 6 1 in place!;
    #nop #config mouse on;
    #screen clear top;
    comm_window_draw_tabs;
    #showme <238>(Ashtan): Fitz says, "Stuff".;
    #showme <238>(Ashtan): Fitz says, "weclome noobs".;
    #showme <238>(Ashtan): Fitz says, "hi".;
    #showme <238>(Ashtan): Fitz says, "by".;
    #showme <238>(Ashtan): Fitz says, "hail".;
    #showme <238>(Ashtan): Fitz says, "greetings".;
    #showme <238>(Ashtan): Fitz says, "help".;
    #showme <238>(Ashtan): Fitz says, "undestand".;
    #showme <238>(Ashtan): Fitz says, "things".;
    #showme <238>(Ashtan): Fitz says, "today".;
    #showme <238>Fitz says, "why not".;
    #showme <128>Pruxi tells you "Stuff".;
}

#EVENT {IAC SB GMCP Comm.Channel.Text IAC SE}
{
   #nop print %0;
   #var messages {%0};
   comm_window_show chats $messages[text]
}
