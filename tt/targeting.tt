#nop Targeting aliases and events

#class targeting kill;
#class targeting open;

#read targetlist.tt;

#alias {targeting.onRoomInfo}
{
   #variable roomInfo $gmcp[room][info];

   #nop UNhighlight mobs from a different area.;
   #if {{$areaName} != {$roomInfo[area]}} {
      #local areaList $targets[$areaName];
      #foreach *areaList[] tar {
         #local tar *areaList[+tar]; 
         #unhighlight $tar; 
      };
   };

   #var areaName $roomInfo[area];

   #nop highlight new area's targets;
   #local areaList $targets[$areaName];
   #foreach *areaList[] tar {
      #nop #show <003> highlighting: [$tar];
      #highlight $tar {blink reverse};
   };
}

#alias {targeting.onCharItemsAdd}
{
   #local icon $gmcp[char][items][add][icon];
   #if {"$icon" == "deadbody"} { #return; };

   #local attri $gmcp[char][items][add][attrib];
   #if {"$attri" == ""}
   { 
      #local name $gmcp[char][items][add][name];
      #local id $gmcp[char][items][add][id];
      #local item <278>$name<028>$id<209>;

      room_window_show items $item;
      #continue; 
   };

   #if {"$attri" != "%*m%*" }{ #continue; };
   #if {"$attri" == "%*d%*" }{ #show <003>dead;#continue; };
   #if {"$attri" == "%*x%*" }{ #show <003>leave alone;#continue; };

   #local name $gmcp[char][items][add][name];
   #local id $gmcp[char][items][add][id];
   #local mob <278>$name<028>$id<209>;
   #var roomMobs[$id] {$name};

   room_window_show mobs $mob;
}

#alias {targeting.onCharItemsList}
{
   #list room_window[tab][mobs] clear;
   #list room_window[tab][items] clear;
   #unvar roomMobs;
   refresh_room_window mobs;

   #local items $gmcp[char][items][list];
   #foreach $items[%*] item {

      #local icon $item[icon];
      #if {"$icon" == "deadbody"} { #continue; };

      #local attri $item[attrib];
      #nop ADD items here;
      #if {"$attri" == {} }
      { 
         #local name $item[name];
         #local id $item[id];
         #local item <278>$name<028>$id<209>;

         #nop #showme <003>Adding: [$item];

         #nop #show <003>NO ATTRIB;
         room_window_show items $item;
         #continue; 
      };

      #if {"$attri" != "%*m%*" }{ #continue; };
      #if {"$attri" == "%*d%*" }{ #show <003>dead;#continue; };
      #if {"$attri" == "%*x%*" }{ #show <003>leave alone;#continue; };

      #local name $item[name];
      #local id $item[id];
      #local mob <278>$name<028>$id<209>;
      #var roomMobs[$id] {$name};

      room_window_show mobs $mob;
   }
}

#alias {targeting.onCharItemsRemove}
{
   #local id $gmcp[char][items][remove][id];
   #unvar roomMobs[$id];
   #nop #show {<403>Deleting mob id: $id};

   #list room_window[tab][mobs] find {%*$id%*} idx;
   #list room_window[tab][mobs] delete $idx;
   #nop #show {<403>Deleting idx from window: $idx};

   refresh_room_window mobs;
   #unvar idx;
}

#alias {target}
{
   #if {"%1" === ""}
   {
      #showme {<848>Targets for <818>[<838>$areaName<818>]};
      #showme {*targets[$areaName][%*]};
   };
   #elseif {&targets[$areaName][%0]}
   {
      #unhighlight {%0};
      #unvar targets[$areaName][%0];
      #showme Target '%0' removed from '$areaName'.;
      #class targetlist write targetlist.tt;
   };
   #else
   {
      #highlight {%0} {blink reverse};
      #var targets[$areaName][%0] {};
      #showme Target '%0' added to '$areaName'.;
      #class targetlist write targetlist.tt;
   };
}

#alias {atk}
{
   #if {"%1" === ""}
   {
      #foreach *roomMobs[] denizen {
         #showme in room: <038>$denizen;
         #foreach *targets[$areaName][] tar {
            #showme trying target: <038>$tar;

            #if {"$roomMobs[$denizen]" == "%*$tar%*"} {
               #showme FOUND tar: <028>$tar;

               #var atkTarget $denizen;
               #send {settarget $denizen};
               attackCmd $denizen;

               #return;
            };
         };
      };
   };
   #else
   {
      #var atkTarget %1;
      attackCmd %1;
   };

   #nop #unvar denizen;
}

#class targeting close
