#nop Targeting aliases and events

#class targeting kill
#class targeting open

#read targetlist.tt

#EVENT {IAC SB GMCP room.info IAC SE}
{
   #local areaList &targets[$areaName];
   #nop #loop 1 &areaList[] tar { #local tar *areaList[+tar]; #unhighlight $tar; };

   #variable roomInfo {%0};
   #var areaName $roomInfo[area];

   #nop highlight new area's targets;
   #local areaList $targets[$areaName];
   #foreach *areaList[] tar {
      #nop #show <003> highlighting: [$tar];
      #highlight $tar {blink reverse};
   };
}

#EVENT {IAC SB GMCP Char.Items.Add IAC SE}
{
   #local item {%0};
   #variable gmcp[char][items][add] $item[item];

   #local attri $gmcp[char][items][add][attrib];
   #if {"$attri" == {} }
   { 
      #local name $gmcp[char][items][add][name];
      #local id $gmcp[char][items][add][id];
      #local item <278>$name<028>$id<209>;

      room_window_show items $item;
      #continue; 
   };

   #if {"$attri" != "%*m%*" }{ #continue; };
   #if {"$attri" == "%*dx%*" }{ #continue; };

   #local name $gmcp[char][items][add][name];
   #local id $gmcp[char][items][add][id];
   #local mob <278>$name<028>$id<209>;

   room_window_show mobs $mob;
}

#EVENT {IAC SB GMCP Char.Items.List IAC SE}
{
   #list room_window[tab][mobs] clear;
   #list room_window[tab][items] clear;
   refresh_room_window mobs;

   #local item {%0};
   #variable gmcp[char][items][list] $item[items];
   #local items $gmcp[char][items][list];
   #foreach $items[%*] item {

      #local attri $item[attrib];
      #nop ADD items here;
      #if {"$attri" == {} }
      { 
         #local name $item[name];
         #local id $item[id];
         #local item <278>$name<028>$id<209>;

         #nop #showme <003>Adding: [$item];

         #nop #show <003>NO ATTRIB;
         room_window_show items $item;
         #continue; 
      };

      #if {"$attri" != "%*m%*" }{ #show <003>Not a monster;#continue; };
      #if {"$attri" == "%*dx%*" }{ #show <003>dead or leave alone;#continue; };

      #local name $item[name];
      #local id $item[id];
      #local mob <278>$name<028>$id<209>;

      #nop #showme <003>Adding: [$mob];

      room_window_show mobs $mob;
   }
}

#EVENT {IAC SB GMCP Char.Items.Remove IAC SE}
{
   #show %0;
   #var gmcp[char][items][remove] %0;

   #local mob $gmcp[char][items][remove][id];
   #list room_window[tab][mobs] find {$mob} idx;
   #unvar room_window[tab][mobs][+$idx];
   refresh_room_window mobs;
}

#alias {target}
{
   #if {"%0" == ""}
   {
      #showme {<848>Targets for <818>[<838>$areaName<818>]};
      #showme {*targets[$areaName][%*]};
   };
   #elseif {&targets[$areaName][%0]}
   {
      #unhighlight {%0};
      #unvar targets[$areaName][%0];
      #showme Target '%0' removed from '$areaName'.;
   };
   #else
   {
      #highlight {%0} {blink reverse};
      #var targets[$areaName][%0] {};
      #showme Target '%0' added to '$areaName'.;
   };
}

#alias {atk} 
{
   #if {"%0" == ""}
   {
      #nop Go through the list of possible targets and attack the first one.;
      #nop #showme <038>Targets list: *targets[$areaName][%*];

      #loop 1 &roomMobs[] denizen {

         #local denizen {$roomMobs[+$denizen]};
         #showme in room: <038>$denizen:*roomMobs[+$denizen];
         attackCmd $denizen;

         #nop TODO convert this to a **loop**;
         #loop 1 &targets[$areaName][] tar {
            #local tar *targets[$areaName][+$tar];

            #nop #showme --- $tar ---;

            #if {"$denizen" == "%*$tar%*"} {
               #nop #showme found tar: <028>$tar\e]R,<178>*denizen[];
               #return;
            };
         };
      };
   };
   #else
   {
      attackCmd %1;
   };
}

#alias {filltar} {
   #unvar roomMobs;

   #loop 1 &gmcp[char][items][list][%*] id {
      #var mob $gmcp[char][items][list][$id];

      #showme $id, $mob;
      #var roomMobs[$id] {{$mob[name]}{$mob[id]}};
   };
   #var roomMobs;
}


#class targeting close
