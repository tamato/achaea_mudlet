#nop Cthul Scripts
#nop All variables belong in Cthul.tt or Global.tt.

#var priestessEnable 1;
#var ruinCost 17;

#class bash open

#high {^You have recovered equilibrium%*} { reverse };
#action {You have recovered equilibrium} {
   atk;

   #if {&{atkTarget} == 0} {
      #class bash clear;
      #if {$distortActive === 1} {da};
   };
};

#class bash close;
#class bash save;
#class bash clear;

#nop F1
#macro {\eOP} { 
   #unvar atkTarget;
   #nop print {stopping auto attack};
   #class bash clear; 
};

#nop S-F1
#macro {\e[1;2P} { 
   #class bash load; 
   print {resuming auto attack};
   #nop #if {$distortActive === 0} {da};
   atk;
};

#nop -------------------------------------------------------------------------
#nop BattleRage
#nop -------------------------------------------------------------------------
#class battlerage kill;
#class battlerage open;

#var chaosgate 1;
#var stagnate 1;
#var harry 1;
#var fluctuate 1;

#nop Around monsters?
#nop there might be a different one around people.
#high {A nearly invisible magical shield forms around%*$} { underscore <FFF0000>}
#action {^A nearly invisible magical shield forms around.*$} 
{ 
   #nop A dizzying beam of energy strikes you as your attack rebounds off of a burly troll guard's shield.;
   #if {&{atkTarget} != 0} {
      #if {$rage >= $ruinCost} {
         #send {ruin}
         print Ruin Shield!;
      }
   }
};

#alias {tryRage}
{
   #math sum {%1 + $ruinCost};
   #if {$rage >= $sum && ${%2} == 1} {
      #var rage 0;
      #var %2 0;
      #send {%2}; 
      print {Used %2, disabled [%2]};
      #delay {%3} {#var %2 {1};print %2 reEnabled};
   };
}

#alias {battlerage.onCharVitals}
{
   #if {&{atkTarget} == 0} {#return};

   #var rage {$gmcp[char][vitals][charstats][2]};
   #regex {$rage} {Rage: %d}
      {#var rage &1} 
      {#show <513>BattleRage: NOT GETTING IT};

   #nop TODO - need to add Temper;

   tryRage 35 chaosgate 23;
   tryRage 24 stagnate 35;
   tryRage 14 harry 17;
   tryRage 23 fluctuate 25;
}

#class battlerage close;
#class battlerage save;

#nop -------------------------------------------------------------------------
#nop Tarot
#nop -------------------------------------------------------------------------
#class tarot kill;
#class tarot open;

#alias {b} { 
   #if {"%1" === ""}  { 
      #send {hermits}
   } { 
      #foreach {$gmcp[char][items][list][items][]} thing {
         #if {"$thing" == "%*mono%*"} {
            print {Monolith present, find another room};
            #return;
         };
      };

      #send {$addfree outd hermit};
      #send {$addfree activate hermit %1};
      print {Can hermit be used off balance?};
      updateCharUI;
   };
};

#alias {t} { 
   #if {"%1" === ""}  { 
      #send {hermits} 
   } { 
      #send {fling hermit at ground %1};
      print {Can hermit be used off balance?};
      updateCharUI;
   };
};

#class priestess kill
#class priestess open

#nop when fling is used.;
#nop You shuffle a tarot card inscribed with the High Priestess out of your deck, bringing the total number of remaining cards to 89;

#nop when outd is used;
#nop You shuffle a card with the image of the High Priestess out of your deck, bringing the total remaining to 86;
#line oneshot #action {High Priestess %* bringing the total %2 to %3.$} {
   #var priCards %3; 
   updateCharUI; 
};

#line oneshot #action {Raising the High Priestess tarot over your head} {
   #var priestessEnable 1; 
   #var priStatus {Ready};

   #nop Have it out of deck, it could be similar to the rift in that some things my block getting it out.;
   #nop TODO Should do this for FOOL too.;
   #nop #send {outd priestess};
   updateCharUI;
   print Healed from Priestess;

   #class priestess clear
};

#class priestess close
#class priestess save
#class priestess clear

#var priStatus {Ready};
#var priCards 0;
#alias {pri} {
   #nop Since this actin is nested in the Alias we HAVE to use an extra %;
   #class priestess load;

   #nop #send {$insfree fling priestess at me};
   #send {$insfree fling priestess at me};
   #var priStatus {Qd};
   #var priestessEnable 0; 
   updateCharUI;
};

#nop Alias only match at the start of a line.;
#nop There is also a comment about if no % var's are used in the commands, something will get appended to %0.;
#alias {uni} {
   #if {"%1" === ""} {
      print {Universe Shortcuts};
      #show {nt : New Thera};
      #show {gn : Genji};
      #show {sh: Shastaan};
      #show {az: Azdun};
      #show {bf: Bitterfork};
      #show {mb: Manara};
      #show {};
   } {
      #send {$addfree fling universe at ground};
      
      #var dest %1;
      
      #if     {"%1" === "nt"} {#var dest {newthera}};
      #elseif {"%1" === "gn"} {#var dest {genji}};
      #elseif {"%1" === "sh"} {#var dest {shastaan}};
      #elseif {"%1" === "az"} {#var dest {azdun}};
      #elseif {"%1" === "bf"} {#var dest {bitterfork}};
      #elseif {"%1" === "mb"} {#var dest {manara}};
      
      #nop example of a one time action;
      #line oneshot {#action {A shimmering, translucent }
      {
         #show {Headed out to $dest};

         #send {$addfree touch $dest};
         #unvar dest;
      }};
   }
};

#nop Fool, 35 sec cooldown;

#nop 18:06:21.90 start;
#nop 8:06:56.61 O ends;
#var foolMaxTimer 35;
#var remainingFoolTime 0;
#alias {fool} {
   #send {$addfree fling fool at me};

   #line oneshot #action {^You press the Fool tarot to your forehead.$}{
      #line oneshot #high {%*} {reverse};
      #var remainingFoolTime $foolMaxTimer;

      #ticker foolTimer {
         #math remainingFoolTime {$remainingFoolTime - 1};
         updateCharUI;

         #if {remainingFoolTime <= 0} {#unticker foolTimer; #var remainingFoolTime 0;};
      } {1};

      updateCharUI;
   };

   #line oneshot #high {^You may heal another affliction$}{reverse};
};

#alias {inscr} {
   #var cardcount 0;
   #if {&{2} == 0} 
      {#var cardcount 20};
      {#var cardcount %1};
   
   #var card %1;
   #send {outd $cardcount blank};
   #send {inscribe blank with $cardcount $card};

   #line oneshot #action {You have successfully inscribed the image of the %%1 on your Tarot card} {
      #line oneshot #high {%%*} {reverse};
      #send {ind all $card};
   };

   #unvar card;
   #unvar cardcount;
};

#alias {dl} {
   #line oneshot #action {{.+}[%%!s%%1] Priestess} {
      #var priCards %%1;
      updateCharUI;
   };

   #line oneshot #action {{.+}[%%!s%%1] Hermit} {
      #var hermitCards %%1;
      updateCharUI;
   };

   #send {dl};
};

#class tarot close;

#alias {trainer}
{
   #send {CITY GUIDE AVAILABLE};
   #send {CITYNEWBIES};
   #send {CITY NOVICE LIST};
}

#var generosity 0;
#alias {self} {
   #if {$generosity === 0} {
      #var generosity 1;
      #send {curing defences on};
   };
   #else {
      #var generosity 0;
      #send {curing defences off};
      #send {generosity};
   };
}

#alias {qq} {
   #send {curing defences off};
   #send {CITY GUIDE UNAVAILABLE};
   #send {QQ};
}

